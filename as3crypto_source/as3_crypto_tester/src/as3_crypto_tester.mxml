<?xml version="1.0" encoding="utf-8"?>
<mx:WindowedApplication xmlns:mx="http://www.adobe.com/2006/mxml" layout="absolute">	
<mx:Script>
	<![CDATA[
	
		import alchemy.samples.as3_crypto_wrapper;
		
		private var aes128key:ByteArray = null;
		private var aes128iv:ByteArray = null;
		
		private function toBase16(input:ByteArray):String
		{
			var output:String = new String;
			const hex:String = "0123456789abcdef";
			const length:int = input.length;
			for(var i:int = 0; i < length; i++) {
				output += hex.charAt((input[i] & 0xF0) >> 4);
				output += hex.charAt(input[i] & 0x0F);
			}
			return output;
		}
		
		private function fromBase16(input:String):ByteArray
		{
			var output:ByteArray = new ByteArray;
			const hex:Object = {'0':0,'1':1,'2':2,'3':3,'4':4,'5':5,'6':6,'7':7,'8':8,'9':9,'a':10,'b':11,'c':12,'d':13,'e':14,'f':15,'A':10,'B':11,'C':12,'D':13,'E':14,'F':15};
			const length:int = input.length;
			var i:int = 0;
			while(i < length) {
				var byte:uint = (hex[input.charAt(i)] << 4);
				i++;
				if(i < length) {
					byte += hex[input.charAt(i)];
					i++;
				}
				output.writeByte(byte); 
			}
			return output;
		}
		
		private function initKeyAndIV():void
		{
			as3_crypto_wrapper.prng_seed(null);
			if(null == aes128key)
				aes128key = as3_crypto_wrapper.prng_random(16);
			if(null == aes128iv)
				aes128iv = as3_crypto_wrapper.prng_random(16);
		}
		
		private function onSHA1():void
		{
			var buffer:ByteArray = new ByteArray;
			buffer.writeUTF(dataText.text);
			var hdl:uint = as3_crypto_wrapper.sha1_begin();
			as3_crypto_wrapper.sha1_update(hdl, buffer);
			var digest:ByteArray = as3_crypto_wrapper.sha1_finish(hdl);
			outputText.text = toBase16(digest);
		}
		
		private function onSHA256():void
		{
			var buffer:ByteArray = new ByteArray;
			buffer.writeUTF(dataText.text);
			var hdl:uint = as3_crypto_wrapper.sha256_begin();
			as3_crypto_wrapper.sha256_update(hdl, buffer);
			var digest:ByteArray = as3_crypto_wrapper.sha256_finish(hdl);
			outputText.text = toBase16(digest);
		}
		
		private function onMD5():void
		{
			var buffer:ByteArray = new ByteArray;
			buffer.writeUTF(dataText.text);
			var hdl:uint = as3_crypto_wrapper.md5_begin();
			as3_crypto_wrapper.md5_update(hdl, buffer);
			var digest:ByteArray = as3_crypto_wrapper.md5_finish(hdl);
			outputText.text = toBase16(digest);
		}
		
		private function onEncryptAES():void
		{
			var buffer:ByteArray = new ByteArray;
			buffer.writeUTF(dataText.text);
			initKeyAndIV();
			var hdl:uint = as3_crypto_wrapper.aes128_cbc_encrypt_begin(aes128key, aes128iv);
			var encrypted:ByteArray = as3_crypto_wrapper.symmetric_encrypt_update(hdl, buffer);
			encrypted.writeBytes(as3_crypto_wrapper.symmetric_encrypt_finish(hdl));
			outputText.text = toBase16(buffer);
			encrypted.position = 0;
			outputText2.text = toBase16(encrypted);
		}
		
		private function onDecryptAES():void
		{
			var buffer:ByteArray = fromBase16(outputText2.text);
			var hdl:uint = as3_crypto_wrapper.aes128_cbc_decrypt_begin(aes128key, aes128iv);
			var decrypted:ByteArray = as3_crypto_wrapper.symmetric_decrypt_update(hdl, buffer);
			decrypted.writeBytes(as3_crypto_wrapper.symmetric_decrypt_finish(hdl));
			outputText.text = toBase16(decrypted);
			decrypted.position = 0;
			outputText2.text = decrypted.readUTF();
		}
		
		private function onGetRandomData():void
		{
			as3_crypto_wrapper.prng_seed(null);
			var buffer:ByteArray = as3_crypto_wrapper.prng_random(20);
			dataText.text = toBase16(buffer);
		}		
		
	]]>
</mx:Script>

	<mx:TextArea id="dataText" right="10" left="10" top="10"/>
	<mx:Text id="outputText" text="&lt;digest&gt;" right="10" y="121" left="10"/>
	<mx:Text id="outputText2" text="&lt;digest&gt;" right="10" y="160" left="10"/>
	<mx:Button x="10" y="62" label="Digest SHA1" id="sha1" click="onSHA1()"/>
	<mx:Button x="115" y="62" label="Digest SHA256" id="sha256" click="onSHA256()"/>
	<mx:Button x="234" y="62" label="Digest MD5" id="md5" click="onMD5()"/>
	<mx:Button x="10" y="62" label="Digest SHA1" click="onSHA1()"/>
	<mx:Button x="115" y="62" label="Digest SHA256" click="onSHA256()"/>
	<mx:Button x="234" y="62" label="Digest MD5" click="onMD5()"/>
	<mx:Button x="10" y="91" label="Encrypt AES" click="onEncryptAES()"/>
	<mx:Button x="114" y="91" label="Decrypt AES" click="onDecryptAES()"/>
	<mx:Button x="234" y="91" label="Get Random Data" click="onGetRandomData()"/>
	
</mx:WindowedApplication>
